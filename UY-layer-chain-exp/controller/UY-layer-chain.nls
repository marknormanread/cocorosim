;;====================================================
;;====================================================
;;
;; UY's implementation of BIODS. 
;; 
;; In boids, each boid follows the following three rules:
;; 1) Collision avoidance, avoid collisions with nearby objects
;; 2) Velocity matching, try to match velocity with nearby boids. 
;; 3) Flock centering, try to stay close to nearby boids. 
;;
;; Orginal boids implementation found on this webpage: http://www.red3d.com/cwr/boids/
;;
;;====================================================
;;====================================================
__includes[ 
  "UY_boids_global_params.nls"
]
 
turtles-own 
[  
  ;; these variables comprise a vector indicating the perceived center of the swarm, relative to the current AUV
  cohesion-sideways ;; positive numbers are right, negative are left
  cohesion-forward  ;; positive numbers are forward, negative are back
  cohesion-up       ;; positive numbers are up, negative are down. 
  
  ;; these variables comprise a vector of prefered movement to avoid hitting other AUVs. 
  separation-sideways
  separation-forward
  separation-up
  
  ;; these variables comprise a vector of preferred movement to match the velocity of other AUVs seen around it (through blue light sensors)
  alignment-sideways
  alignment-forward
  alignment-up

  swarm-in-range         ;; used to indicate when the swarm is sensed within range. AUVs may remain stationary under two conditions, 
                         ;; the swarm-related forces acting upon it dictate that it should remain so, or it cannot perceive any of the swarm, 
                         ;; and so remains still. In the latter case, this variable is set to ensure the AUV moves. 
  swarm-in-range-data-collection
  
  ;; these are temp stores of distance to other AUVs registered on the sensors, used to prevent unnecessary double computation
  distance-front
  distance-front-right 
  distance-front-left
  distance-back 
  distance-back-right
  distance-back-left
  distance-up  
  distance-down
  
  chain-layered           ;; boolean, whether or not this AUV comprises a layer in the chain
  chain-layer-depth       ;; depth in meters at which this AUV resides
  chain-stationary        ;; boolean, set to true this AUV will not move
]


;;========================================================
;; Called by the OS to step the controller. 
;;========================================================
to Controller-update
  set distance-front        OS_sens_BL_front
  set distance-front-right  OS_sens_BL_front_right
  set distance-front-left   OS_sens_BL_front_left
  set distance-back         OS_sens_BL_back
  set distance-back-right   OS_sens_BL_back_left
  set distance-back-left    OS_sens_BL_back_right
  set distance-up           OS_sens_BL_up
  set distance-down         OS_sens_BL_down  
  
  let cohesion-weight   boids-cohesion-weight
  let separation-weight boids-separation-weight
  let alignment-weight  boids-alignment-weight
  let maximum-speed     boids-max-speed
  
  let neighbours OS_sens_auvs_RF
  
  ifelse chain-layered = false
  [
    set color yellow
    set cohesion-weight    1.0
    set separation-weight  1.0 
    set alignment-weight   3.0
    set maximum-speed      0.5
    
    ;; members of the exploratory shoal should not receive heading information from chain members
    set neighbours OS_sens_auvs_RF with [chain-layered = false]
  ]  
  [
    set color black
    ;set color yellow
  ]

;;For now to make video consistent with the chain change top and bottom to sky blue 
;;This is not a good way of doing the bottom link - needs to be dynamic to adapt to length of chain
  let lowest_depth 0
  ask AUVs-Lily with-max [ chain-layer-depth ]
    [ set color sky]
   ;;Not min should eb min -1 not sure how to do! 
   ask AUVs-Lily with-min [ chain-layer-depth ]
   [set lowest_depth chain-layer-depth]
   
   ;show lowest_depth 
   ask AUVs-Lily with [ chain-layer-depth = lowest_depth + 0.25]
   [set color sky]
  
  ifelse count neighbours != 0
  [  
   
    set swarm-in-range true
    
    calculate-cohesion-sensor-triangulation
    calculate-separation-sensor-triangulation
    calculate-alignment  neighbours 


    ;; take averages, and normalise
    let sum-weights (boids-cohesion-weight + boids-separation-weight + boids-alignment-weight)
    let collective-forward (( (cohesion-weight   * cohesion-forward  ) 
                            + (separation-weight * separation-forward) 
                            + (alignment-weight  * alignment-forward )) 
                            /  sum-weights) 
    let collective-sideways ((  (cohesion-weight   * cohesion-sideways  ) 
                              + (separation-weight * separation-sideways) 
                              + (alignment-weight  * alignment-sideways )) 
                              / sum-weights)
    
    let collective-up   ((  (cohesion-weight * cohesion-up) 
                           + (separation-weight * separation-up) 
                           + (alignment-weight * alignment-up)) 
                           /  sum-weights) 

    ;; desired heading is relative to the boid. Desired speed is interpreted as a percentage of maximum thrust
    let desired-heading calculate-desired-heading collective-forward  collective-sideways  ;; This is relative to the AUV
    let desired-speed   calculate-desired-speed   collective-forward  collective-sideways 
    set desired-speed min (list (boids-speed-scalar * desired-speed) maximum-speed)  ;; upper limit on what desired speed can be
      
    if not chain-stationary
    [ OS_movement_arc (OS_orient_rel_to_abs_heading desired-heading) desired-speed boids-turn-gain ]
   
      ;; ensuring that values passed to buoyancy actuators do not exceed sensible values
    set collective-up collective-up * 0.1
   ; ifelse chain-layered
   ; [ 
      OS_buoyancy_set_depth chain-layer-depth 
   ; ]
   ; [ OS_buoyancy_set_speed collective-up ]
  ] 
  [   ;; if no neighbours are percieved, colour red. OS will continue to drive AUV in same direction.
    set color red 
    set swarm-in-range false
    OS_movement_arc (heading) (0.8 * maximum-speed) boids-turn-gain
  ] 
end



;;====================================================
;; Calculates the center of the swarm using sensor triangulation - distance but no exact angle.
;;====================================================
to calculate-cohesion-sensor-triangulation
  let noise-threshold OS_bluelight_range * 0.95  ;; thresholding to avoid undesirable influence of sensor noise   
  
  ;; take local copy, since it is going to be modified, to prevent interference with other rules
  let dist-front       distance-front
  let dist-front-right distance-front-right
  let dist-front-left  distance-front-left 
  let dist-back        distance-back       
  let dist-back-right  distance-back-right
  let dist-back-left   distance-back-left  
  let dist-up          distance-up         
  let dist-down        distance-down    

  ;; distance indicators indicate maximum when there is nothing perceived. Thresholding places the center of the swarm on this AUV where that is the case 
  if dist-front-right > noise-threshold  [  set dist-front-right 0  ]
  if dist-front-left  > noise-threshold  [  set dist-front-left 0  ]
  if dist-back        > noise-threshold  [  set dist-back  0  ]
  if dist-up          > noise-threshold  [  set dist-up    0  ]
  if dist-down        > noise-threshold  [  set dist-down  0  ]
  
  if dist-front       > noise-threshold  [  set dist-front 0   ]
  if dist-back-right  > noise-threshold  [  set dist-back-right 0   ]
  if dist-back-left   > noise-threshold  [  set dist-back-left 0   ]
    
    ;; based on what sensor observation and orientation, project onto forward-axis (relative to robot)
  set cohesion-forward project-forward  60 0 1.0 dist-front-right
                     + project-forward -60 0 1.0 dist-front-left
                     + project-forward 180 0 1.0 dist-back
                         
  set cohesion-sideways project-sideways 60 0  1.0 dist-front-right 
                      + project-sideways -60 0 1.0 dist-front-left
                                            
  set cohesion-up      project-up 0 90  1.0 dist-up 
                     + project-up 0 -90 1.0 dist-down   
                     
  if six-sensor 
  [ 
    set cohesion-forward cohesion-forward   + project-forward 0 0 1.0 dist-front 
                                            + project-forward   120 0 1.0 dist-back-right
                                            + project-forward  -120 0 1.0 dist-back-left
                                            
    set cohesion-sideways cohesion-sideways + project-sideways  120 0 1.0 dist-back-right
                                            + project-sideways -120 0 1.0 dist-back-left                                           
  ]   
end


;;====================================================
;; Computes the separation rule
;;====================================================
to calculate-separation [ neighbours ]
  ;; these are calculated by considering each neighbouring boid in turn
  set separation-sideways 0
  set separation-forward 0
  set separation-up 0

  ask neighbours
  [
    let dist distance-nowrap myself   ;; distance from the neighbour to the calling current boid   
    let infringement (boids-separation-threshold / patchlength) - dist   ;; quantity by which distance is less than the threshold
    if infringement > 0  
    [  ;; in this case the neighbour was closer than the threshold. Do nothing otherwise, because the neighbour is outside the threshold
       let diffx xcor - [xcor] of myself  ;; these differences are absolute, not relative to the auv. Distance between infringing boid and current boid
       let diffy ycor - [ycor] of myself
       let diffz zcor - [zcor] of myself 
             
       let abs-pitch atan diffz (sqrt (diffx ^ 2) + (diffy ^ 2))   ;; the pitch angle to infringing boid from current boid
       let abs-heading 0
       if not (diffx = 0 and diffy = 0)    ;; safety, in case one AUV is directly above another. 
       [     
         set abs-heading atan (diffx) (diffy)  ;; absolute heading towards the infringing neighbour
       ]       
       ask myself ;; the following computations have to be done in the current boid, not the infringing boid
       [         
         let rel-heading OS_orient_abs_to_rel_heading abs-heading   ;; the heading to the infringing neighbour relative to the current boid
 
         let inf-x (sin rel-heading) * (cos abs-pitch) * infringement  ;; calculate the vector of infringement past the thrshold in x,y,z planes RELATIVE TO BOID
         let inf-y (cos rel-heading) * (cos abs-pitch) * infringement
         let inf-z (sin abs-pitch) * infringement   
         
         set separation-sideways separation-sideways - inf-x  ;; sum this infringements to the final movement vector
         set separation-forward separation-forward - inf-y
         set separation-up separation-up - inf-z
       ]       
    ]
  ]
end

;;====================================================
;; Calculates the separation rule using sensor triangulation. Distance within a sensor cone is knwon, but the exact relative location/angle is not. 
;;====================================================
to calculate-separation-sensor-triangulation
  set separation-sideways 0
  set separation-forward 0
  set separation-up 0
  
  ;; statements that check if AUV distance is closer than threshold.
  ;; Infringements of AUVs closer than the threshold should result in the AUV moving away. As such, the projected 
  ;; vector is SUBTRACTED from the avoidance-vector as it is compiled (rather than summed, as elsewhere).
  if distance-front-right < boids-separation-threshold
  [ 
    set separation-forward  separation-forward  - project-forward  60 0 1.0 (boids-separation-threshold - distance-front-right) 
    set separation-sideways separation-sideways - project-sideways 60 0 1.0 (boids-separation-threshold - distance-front-right)
  ] 
    
  if distance-front-left  < boids-separation-threshold
  [ 
    set separation-forward  separation-forward  - project-forward  -60 0 1.0 (boids-separation-threshold - distance-front-left) 
    set separation-sideways separation-sideways - project-sideways -60 0 1.0 (boids-separation-threshold - distance-front-left)
  ]
  
  if distance-back < boids-separation-threshold
  [ set separation-forward separation-forward - project-forward 180 0 1.0 (boids-separation-threshold - distance-back) ]    
    
  if distance-up          < boids-separation-threshold
  [  set separation-up separation-up - project-up 0 90 1.0 (boids-separation-threshold - distance-up) ]
  
  if distance-down        < boids-separation-threshold
  [ set separation-up separation-up - project-up 0 -90 1.0 (boids-separation-threshold - distance-down) ] 
  
  if six-sensor
  [
    if distance-front      < boids-separation-threshold
    [ set separation-forward separation-forward - project-forward 0 0 1.0 (boids-separation-threshold - distance-front) ]
    
    if distance-back-right < boids-separation-threshold
    [ 
      set separation-forward  separation-forward  - project-forward  120 0 1.0 (boids-separation-threshold - distance-back-right)
      set separation-sideways separation-sideways - project-sideways 120 0 1.0 (boids-separation-threshold - distance-back-right) 
    ]
    
    if distance-back-left  < boids-separation-threshold
    [ 
      set separation-forward  separation-forward  - project-forward   120 0 1.0 (boids-separation-threshold - distance-back-left)
      set separation-sideways separation-sideways - project-sideways -120 0 1.0 (boids-separation-threshold - distance-back-left) 
    ]
  ]
end

;;====================================================
;; calculates the alignment rule using global triangulation. 
;;====================================================
to calculate-alignment [ neighbours ]
  ;; obtain lists of each neighbours velocity in the x, y and z axes (this is in absolute space)
  let vel-xs [ auv-velocity-x ] of neighbours
  let vel-ys [ auv-velocity-y ] of neighbours
  let vel-zs [ auv-velocity-z ] of neighbours
  
  ;; get the average velocity (in absolute space). Divide by patchlength because all the other measures are in patches, not meters. 
  let abs-x median vel-xs / patchlength
  let abs-y median vel-ys / patchlength
  let abs-z median vel-zs / patchlength
  
  ;; calculate the average velocity relative to the boid
  set alignment-forward    ((sin heading) * abs-x) + ((cos heading) * abs-y) 
  set alignment-sideways   ((cos heading) * abs-x) + ((-1 * sin heading) * abs-y)
  set alignment-up         abs-z
end


;;====================================================
;; basic trigonometry, will calculate the magnitude of the desired velocity (the speed) in the horrizontal plane
;;====================================================
to-report calculate-desired-speed [ collective-forward collective-sideways ]
  report sqrt ((collective-forward ^ 2) + (collective-sideways ^ 2))
end

;;====================================================
;; basic trigonometry, will calculate the heading of the desired heading of the AUV, relative to the direction in which the AUV faces
;;====================================================
to-report calculate-desired-heading [ collective-forward collective-sideways ]
  if collective-forward = 0 and collective-sideways = 0
  [ report 0 ] ;; default to straight ahead
  report atan collective-sideways collective-forward
end

;;====================================================
;; projects vector of given magnitude, heading and pitch onto the vector along which an AUV points. 
;; The projection can be scaled by the given scalar. 
;;   sensor-heading - heading of vector relative to AUV
;;   sensor-pitch   - pitch of vector relative to AUV
;;   scale-factor   - the projection is multiplied by this factor
;;   sensor-reading - the magnitude of the vector
;;
;; Note that a heading of 0 points in the same direction of the AUV, and a pitch of 90 points stright upwards. 
;;====================================================
to-report project-forward [ sensor-heading sensor-pitch scale-factor sensor-reading ]
  report (cos sensor-pitch) * (cos sensor-heading) * sensor-reading * scale-factor
end

;;====================================================
;; projects vector of given magnitude, heading and pitch onto the vector sideways to where the AUV points (left to right). 
;; The projection can be scaled by the given scalar. 
;;   sensor-heading - heading of vector relative to AUV
;;   sensor-pitch   - pitch of vector relative to AUV
;;   scale-factor   - the projection is multiplied by this factor
;;   sensor-reading - the magnitude of the vector
;;
;; Note that a heading of 0 points in the same direction of the AUV, and a pitch of 90 points stright upwards. 
;;====================================================
to-report project-sideways [ sensor-heading sensor-pitch scale-factor sensor-reading ]
  report (cos sensor-pitch) * (sin sensor-heading) * sensor-reading * scale-factor
end


;;====================================================
;; projects vector of given magnitude, heading and pitch onto the vector upwards with respect to where the AUV points. 
;; The projection can be scaled by the given scalar. 
;;   sensor-heading - heading of vector relative to AUV
;;   sensor-pitch   - pitch of vector relative to AUV
;;   scale-factor   - the projection is multiplied by this factor
;;   sensor-reading - the magnitude of the vector
;;
;; Note that a heading of 0 points in the same direction of the AUV, and a pitch of 90 points stright upwards. 
;;====================================================
to-report project-up [ sensor-heading sensor-pitch scale-factor sensor-reading ]
  report (sin sensor-pitch) * sensor-reading * scale-factor
end


to become-exploratory-shoal
  ask AUVs-Lily with-min [chain-layer-depth]
  [
    set chain-layered false
  ]
end


;;====================================================
;; Called by every AUV to set up. 
;;====================================================
to Controller-setup
  controller-setup-parameters
   
  set cohesion-sideways 0 ;; positive numbers are right, negative are left
  set cohesion-forward 0  ;; positive numbers are forward, negative are back
  set cohesion-up 0       ;; positive numbers are up, negative are down. 
   
  set separation-sideways 0
  set separation-forward 0
  set separation-up 0 
   
  set alignment-sideways 0
  set alignment-forward 0
  set alignment-up 0
  
  set swarm-in-range true
  set swarm-in-range-data-collection true
  
  set chain-stationary false
  
end
